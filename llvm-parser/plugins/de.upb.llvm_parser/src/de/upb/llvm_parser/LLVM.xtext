grammar de.upb.llvm_parser.LLVM with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore"
generate llvm "http://www.upb.de/llvm_parser/llvm"

LLVM:
	elements+=AbstractElement*;

NUMBER returns EBigDecimal:
	'-'? INT ('.' INT)?;

terminal VALID_ID:
	((('%' | '@') '.'*
		(
			(('a'..'z' | 'A'..'Z' | '_' | '0'..'9')+) | STRING
		)
	) | ('(' VALID_ID POINTER? ')'))
	
	('.'
		(
			( ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')+) |('(' ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')+ POINTER? ')')
		)
	)*;

terminal INT_TYPE:
	'i' ('0'..'9')+ POINTER?;
	
terminal BOOL:
	'true' | 'false'
;
	
terminal PRIMITIVE_VALUE:
		'void' | 'null' | 'label' | 'undef' | '...' ;
		

		
terminal FLOATING_POINT_TYPE:
	'half' | 'float' | 'double' | 'x86_fp80' | 'fp128' | 'ppc_fp128';
	

terminal INITIALIZER:
	'zeroinitializer';

terminal STRING:
	'c'?'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '0' | '"' | "'" | '\\') | !('\\' | '"'))* '"' |
	'c'?"'" ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '0' | '"' | "'" | '\\') | !('\\' | "'"))* "'";

terminal UNKNOWN_TYPE:
	'(...)' POINTER?;

terminal SL_COMMENT:
	';' !('\n' | '\r')* ('\r'? '\n')?;

terminal POINTER:
	'*'+;
	
terminal METADATA:
	'!'('a'..'z' | 'A'..'Z' | '_' | '0'..'9')+ 
	| '!"' -> '"'
;
	
//Key Words
terminal F_PREDICATES:
	'oeq' | 'ogt' | 'oge' | 'olt' | 'ole' | 'one' | 'ord' |
	'ueq' | 'ugt' | 'uge' | 'ult' | 'ule' | 'une' | 'uno' ;
	
terminal ATOMIC_ORDERING:
	'unordered' | 'monotonic' | 'aquire' | 'release' | 'acq_rel' | 'seq_cst';

terminal BIN_OP:
	('xchg' | 'add' | 'sub' | 'and' | 'nand' | 'or' | 'xor' | 'max' | 'min' | 'umax' | 'umin');

terminal RETURN_ATTRIBUTES:
	'zeroext' | 'signext' | 'inreg' | 'byval' | 'sret' | 'noalias' | 'nocapture' | 'nest';

CallingConv:
	'ccc' | 'fastcc' | 'coldcc' | 'x86_stdcallcc' | 'x86_fastcallcc' | 'x86_thiscallcc' | 'arm_apcscc' | 'arm_aapcscc' |
	'arm_aapcs_vfpcc' | 'msp430_intrcc' | 'ptx_kernel' | 'ptx_device' | 'spir_func' | 'spir_kernel' | 'cc' '<' NUMBER '>'
	NUMBER;

FUNCTION_ATTRIBUTES:
	'address_safety' | 'alignstack' '(' '<' NUMBER '>' ')' | 'alwaysinline' | 'nonlazybind' | 'inlinehint' | 'naked' |
	'noimplicitfloat' | 'noinline' | 'noredzone' | 'noreturn' | 'nounwind' | 'optsize' | 'readnone' | 'readonly' |
	'returns_twice' | 'ssp' | 'sspreq' | 'uwtable';

terminal CAST_OP:
	'trunc' | 'zext' | 'sext' | 'fptrunc' | 'fpext' | 'fptoui' | 'fptosi' | 'uitofp' | 'sitofp' | 'ptrtoint' | 'inttoptr'
	| 'bitcast';

terminal I_PREDICATES:
	'eq' | 'ne' | 'ugt' | 'uge' | 'ult' | 'ule' | 'sgt' | 'sge' | 'slt' | 'sle';


terminal VISIBILITY:
	'default' | 'hidden' | 'protected';

terminal LINKAGE:
	'private' | 'linker_private' | 'linker_private_weak' | 'available_externally' | 'linkonce' | 'common' | 'appending' |
	'extern_weak' | 'linkonce_odr' | 'linkonce_odr_auto_hide' | 'dllimport' | 'dllexport' ;

terminal ALIAS_LINKAGE:
	'external' | 'internal' | 'weak' | 'weak_odr';

KEYWORDS:
	ALIAS_LINKAGE | ATOMIC_ORDERING | BIN_OP | RETURN_ATTRIBUTES | CAST_OP | I_PREDICATES | VISIBILITY | LINKAGE 
;

LABEL_ID:
	(KEYWORDS | INT_TYPE|ID|BOOL  | 'catch' | 'invoke' | 'resume' | 'switch') ('.' (KEYWORDS|INT_TYPE|ID|BOOL | 'catch' | 'invoke' | 'resume' | 'switch'))*;
	


AbstractElement:
	TopLevelEntity SL_COMMENT? |
	MainLevelEntity SL_COMMENT?;

TopLevelEntity:
	'module' 'asm' module=STRING |
	'target' ('datalayout' | 'triple') '=' target=STRING |
	'deplibs' '=' '[' libs+=STRING (',' libs+=STRING)* ']';

	/*
 * This rule is used to devide the Function Definitions form the Variable Definitions.
 */
MainLevelEntity:
	TypeDefinition
	| GlobalDefinition
	| FunctionDefinition
	| AliasDefinition
	| MetadataValue
	;

TypeDefinition:
	(address=Address '=' (('type' (struct=Structure | 'opaque'))));

GlobalDefinition:
	adress=Address '=' (LINKAGE | ALIAS_LINKAGE | 'global' | 'unnamed_addr' | 'constant')* type = (TypeUse|Aggregate_Types) value=(ValueStruct|Value)?
	(',' 'align' align = NUMBER)?;

FunctionDefinition:
	('define' (LINKAGE| ALIAS_LINKAGE)? VISIBILITY? CallingConv? RETURN_ATTRIBUTES? returnType=TypeUse
	address=Address parameter=FunctionParameterList 'unnamed_addr'? FUNCTION_ATTRIBUTES* ('section ' STRING)? ('align' align = NUMBER)?
	body=FunctionBody) 
	| 
	('declare' (LINKAGE| ALIAS_LINKAGE)? VISIBILITY? CallingConv? RETURN_ATTRIBUTES? returnType=TypeUse
	address=Address parameter=FunctionParameterList 'unnamed_addr'? FUNCTION_ATTRIBUTES* ('section ' STRING)? ('align' align = NUMBER)?);

AliasDefinition:
	address=Address '=' 'alias' ALIAS_LINKAGE? VISIBILITY? aliastype=TypeUse aliasvalue=Value aliasee=(TypeUse);



TypeUse:
	(Predefined | AddressUse) functionInput=UNKNOWN_TYPE?
	;

AddressUse:
	address=[Address|VALID_ID] (pointer=POINTER)?
	;

Address:
	name=VALID_ID;

Predefined:
	type=(PRIMITIVE_VALUE | INT_TYPE | FLOATING_POINT_TYPE) (pointer=POINTER)?
	;




Vector:
	'<' length=NUMBER 'x' type=TypeUse '>';

Array:
	{Array} ('[' length=NUMBER 'x' type=TypeUse ']' | '[]' | '[' ']') POINTER?;

Aggregate_Types:
	Structure | Array | Vector;

Constant:
	value = NUMBER;
	
PrimitiveValue:
	value = (STRING| PRIMITIVE_VALUE | INITIALIZER | BOOL);

Value:
	 NestedCast | NestedGetElementPtr | Constant | AddressUse | PrimitiveValue;

ValueStruct:
	'{' types+=TypeUse value+=Value (',' types+=TypeUse value+=Value)* '}';

Structure:
	{Structure} '{' (types+=TypeUse (',' types+=(TypeUse|Aggregate_Types))*)? '}' POINTER?;


Parameter:
	type = TypeUse RETURN_ATTRIBUTES* value = Value?
;

ParameterList:
	{ParameterList}
	('()' | '('	((params+=Parameter (',' params+=Parameter)*)?) ')');

FunctionParameter:
	type = TypeUse RETURN_ATTRIBUTES* (value = Address)?
;

FunctionParameterList:
	{FunctionParameterList} 
	( '()'   
		| '(' ( params+=FunctionParameter (',' params+=FunctionParameter)*)? ')'
	);


FunctionBody:
	meta+=MetadataValue*
	'{' blocks+=BasicBlock+ '}'
	;
Instruction:
	Store |  Load | GetElementPtr | Alloc | CmpXchg | AtomicRMW    
	| Fence | Return | Branch | Switch | IndirectBranch | Invoke | Resume | Unreachable 
	| LogicOperation | ArithmeticOperation | Compare 
	| Cast | ShuffleVector | InsertElement | ExtractElement | InsertValue | ExtractValue  
	| Phi | Select | Call | VariableAttributeAccess | LandingPad
;
BasicBlock:
	(label=LABEL_ID ':')
	(instructions+= Instruction)+
;


ArithmeticOperation:
	result=Address '=' operation = ('add' | 'fadd' | 'sub' | 'fsub' | 'mul' | 'fmul' | 'udiv' | 'sdiv' | 'fdiv' | 'urem' | 'srem' | 'frem') (('nsw' |
	'nuw') ('nuw' | 'nsw')?)?
	optype=TypeUse value1=Value ',' value2=Value;

LogicOperation:
	result=Address '=' operation = ('shl' | 'lshr' | 'ashr' | 'and' | 'or' | 'xor') optype=TypeUse value1=Value ',' value2=Value;

Cast:
	result=Address '=' operation = CAST_OP from=(TypeUse|Aggregate_Types) value=Value 'to' to=TypeUse;

NestedCast: 
	operation = CAST_OP '(' from=(TypeUse|Aggregate_Types) value=Value 'to' to=TypeUse ')' 
;

MetaArgValue:
	arg=METADATA value=(METADATA|STRING)
;
Meta:
	'metadata' meta=(METADATA) | arg=Predefined? value=Value
;

MetadataValue:
	nodeId=METADATA	'=' 
	'metadata'? ('!' '{' | '!{')  values+=Meta (',' values+=Meta)* '}'
; 


GetElementPtr:
	result=Address '=' 'getelementptr' 'inbounds'? 
		(aggregate=(TypeUse|Array) aggregatename=Value(',' indTypes+=TypeUse indizies+=Value)*)
;
NestedGetElementPtr:
	'getelementptr' 'inbounds'? 
		('('aggregate=(TypeUse|Array) aggregatename=Value(',' indTypes+=TypeUse indizies+=Value)*')')
;
	
ExtractValue:
	result=Address '=' 'extractvalue' (array=Array | struct=Structure) value=Value (',' index+=NUMBER)+;

InsertValue:
	result=Address '=' 'insertvalue' struct=Structure value=Value ',' inserttype=TypeUse insertvalue=Value (',' index+=NUMBER)+;

Fence:
	'fence' 'singlethread'? ordering=ATOMIC_ORDERING;

CmpXchg:
	result=Address '=' 'cmpxchg' (volatile?='volatile')? adresstype=TypeUse adress=Value ',' comparetype=TypeUse comparevalue=Value ',' newtype=TypeUse
	newvalue=Value 'singlethread'?
	ordering=ATOMIC_ORDERING;

AtomicRMW:
	result=Address '=' 'atomicrmw' (volatile?='volatile')? operation = BIN_OP adresstype=TypeUse adress=Value ',' optype=TypeUse opvalue=Value 'singlethread'?
	ordering=ATOMIC_ORDERING;

Load:
	result=Address '=' ( volatile?='volatile' 'load'  | 'load' (volatile?='volatile')?) address=Parameter (',' 'align' align = NUMBER)?  (',' meta+=MetaArgValue)* 
	|
	result=Address '=' 'load' atomic?='atomic' (volatile?='volatile'?)? address=Parameter 'singlethread'? ordering=ATOMIC_ORDERING (',' 'align' align = NUMBER)?;

Store:
	(volatile?='volatile' 'store' | 'store' (volatile?='volatile')?) targetAddress=Parameter ',' value=Parameter  (',' 'align' align = NUMBER)? (',' meta+=MetaArgValue)*     
	|
	'store' atomic?='atomic' (volatile?='volatile')? targetAddress=Parameter ',' value=Parameter 'singlethread'? ordering=ATOMIC_ORDERING (',' 'align' align = NUMBER)?;

Call:
	(result=Address '=')? 'tail'? 'call' CallingConv? RETURN_ATTRIBUTES? adresstype=TypeUse ((adress=Value) | 'asm' 'sideeffect'? STRING ','
	STRING) pList=ParameterList FUNCTION_ATTRIBUTES* (',' '!srcloc' '!' NUMBER)*;

Alloc:
	result=Address '=' 'alloca' ((type=TypeUse typelist=ParameterList?)) (',' numelementstype=TypeUse numelementsvalue=Value)? (',' 'align' align = NUMBER)?;

PhiCase:
	value= Value ',' label = VALID_ID
;

Phi:
	result=Address '=' 'phi' type=TypeUse '[' cases+=PhiCase ']' (',' '[' cases+=PhiCase ']')*;

LandingPad:
	result=Address '=' 'landingpad' struct=Structure 'personality' personalitytype=TypeUse
	personalityvalue=Value ('cleanup' clause+=Clause* | clause+=Clause+);

Clause:
	'catch' type=TypeUse value=Value 
	| 'filter' filterarray=Array (constant=Value);

Select:
	result=Address '=' 'select' conditiontype=TypeUse conditionvalue=Value ',' val1type=TypeUse val1value=Value ',' val2type=TypeUse
	val2value=Value;

VariableAttributeAccess:
	result=Address '=' 'va_arg' listtype=TypeUse listvalue=Value ',' argType=TypeUse;

ExtractElement:
	result=Address '=' 'extractelement' vectortype=TypeUse vectorvalue=Value ',' indextype=TypeUse index=Value;

InsertElement:
	result=Address '=' 'insertelement' vectortype=TypeUse vectorvalue=Value ',' scalartype=TypeUse scalarvalue=Value ',' indextype=TypeUse
	index=Value;

ShuffleVector:
	result=Address '=' 'shufflevector' val1type=TypeUse val1value=Value ',' val2type=TypeUse val2value=Value ',' masktype=TypeUse
	maskvalue=Value;

Compare:
	result=Address '=' ('icmp' pred = I_PREDICATES comptype=TypeUse value1=Value ',' value2=Value |
	'fcmp' pred = (F_PREDICATES| BOOL) comptype=TypeUse value1=Value ',' value2=Value);

IndirectBranch:
	'indirectbr' adresstype=TypeUse adress=Value ',' '[' (labeltype+=TypeUse labels+=Value (',' labeltype+=TypeUse labels+=Value)*)? ']';

Switch:
	'switch' comptype=TypeUse compvalue=Value ',' defaulttype=TypeUse defaultvalue=Value '[' (jtypes+=TypeUse jvalues+=Value ',' destinationtypes+=TypeUse destinations+=Value)+ ']';

Invoke:
	'invoke' CallingConv? RETURN_ATTRIBUTES? functiontype=TypeUse name=Address pList=ParameterList
	FUNCTION_ATTRIBUTES* 'to'
	'label' toTarget=VALID_ID 'unwind' 'label'unwindCase=VALID_ID;

Resume:
	'resume' resumestruct=Structure resumevalue=Value;

Unreachable:
	{Unreachable} 'unreachable';

Return:
	'ret' returntype=Predefined (',' '!dbg' '!' NUMBER)? | 'ret' returntype=TypeUse returnvalue=Value (',' '!dbg' '!' NUMBER)?;

Branch:
	('br' 'label' destination=VALID_ID) | 
	('br' INT_TYPE condvalue=Value ',' 'label'	labelTrue=VALID_ID	',' 'label' labelFalse=VALID_ID);

